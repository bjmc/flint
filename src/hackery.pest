file = {
    title_page? ~ body_content ~ EOI
}

title_page   = { start ~ (multiline_pair | pair ~ NEWLINE)+ }
body_content = { (
    page_break |
    boneyard |
    scene_heading |
    lyrics |
    transition |
    dialogue |
    action |
    blank_line
)+ }

// Title page
char           =  { (ASCII_ALPHANUMERIC | " " | "-" | "(" | ")" | "," | ".") }
key            = @{ char+ }
value          = @{ (emphasis | tail+) }
pair           =  { key ~ ":" ~ value }
multiline_pair =  { key ~ ":" ~ NEWLINE ~ block_value }
indentation    = _{ (" " | "\t")+ }
block_value    =  {
    PUSH(indentation) ~ value ~ NEWLINE ~ (PEEK ~ value ~ NEWLINE)* ~ DROP
}
// END title page

lyrics     = { start ~ "~" ~ text ~ &NEWLINE }
page_break = { start ~ "="{3, } ~ &NEWLINE }

// Basics
whitespace = _{ (" " | "\t") }
indent     = _{ whitespace }
start      =  _{ SOI | NEWLINE }
blank_line =  { NEWLINE }
head       =  { !(NEWLINE | whitespace) ~ ANY }
tail       =  { !(NEWLINE) ~ ANY }
text       = @{ (emphasis | head ~ tail*) }

// emphasis
ul             =  { "_" }
underline      = @{ ul ~ (!ul ~ tail)+ ~ ul }
bi             =  { "***" }
bold_italics   = @{ bi ~ (!"*" ~ tail)+ ~ bi }
b              =  { "**" }
bold           = @{ b ~ (!"*" ~ tail)+ ~ b }
i              =  { "*" }
italics        = @{ i ~ (!"*" ~ tail)+ ~ i }
emphasis_chars =  { ul | bi | b | i }
emphasis       =  { underline | bold_italics | bold | italics }

// Scene headers
scene_heading = { start ~ (force_scene | header_flag) ~ (!"#" ~ text) ~ scene_number? ~ &NEWLINE }
force_scene   = { "." }
scene_number  = { "#" ~ (ASCII_DIGIT | "." | "-")+ ~ "#" }
header_flag   = { (^"INT" | ^"EXT" | ^"EST" | ^"INT./EXT" | ^"INT/EXT" | ^"I/E") ~ (" " | ".")+ }

// Boneyard
boneyard = { "/*" ~ (boneyard | !"*/" ~ ANY)* ~ "*/" }
COMMENT = _{ boneyard }

// Actions

action_text     = @{ (emphasis | tail+) }
centered_action =  { ">" ~ (!"<" ~ ANY)+ ~ "<" }
forced_action   =  { "!" ~ action_text }
generic_action  =  { text }
action          =  { start ~ (forced_action | centered_action | generic_action) ~ &NEWLINE }

// Characters and dialogue
dialogue       = { start ~ character ~ (continuation_line | speech)+ }
speech         = { (NEWLINE ~ indent* ~ (parenthetical | text)) ~ &NEWLINE }
character_name = { (("@" ~ text) | ASCII_ALPHA_UPPER ~ (ASCII_ALPHA_UPPER | ASCII_DIGIT)*) }
dual_marker    = { "^" }
continuation_line = { NEWLINE ~ " "{2,}~ &NEWLINE}
parenthetical  = { "(" ~ (!")" ~ ANY)+ ~ ")" }
character      = { indent* ~ character_name ~ whitespace* ~ (parenthetical | dual_marker)* ~ &NEWLINE }

// Transitions
transition        =  { start ~ emphasis_chars? ~ (forced_transition | magic_transition | to_transition) ~ emphasis_chars? ~ whitespace* ~ &NEWLINE }
forced_transition = @{ ">" ~ transition_txt+ }
to_transition     = @{ leading* ~ "TO:" }
terminal_punctuation = { ("." | ":") }
magic_phrases     =  { "CUT TO BLACK" | "FADE TO BLACK" | "FADE OUT" | "FADE IN" }
magic_transition  = @{ leading* ~ magic_phrases ~ terminal_punctuation? }
leading           =  { !("TO:" | magic_phrases) ~ (indent | transition_txt) }
transition_txt    =  { !(NEWLINE) ~ ANY }
