//! A parser for .Fountain files.

file = {
    title_page? ~ body_content ~ EOI
}

title_page   = { start ~ (multiline_pair | pair ~ NEWLINE)+ }
body_content = { (dialogue | line_element | blank_line)+ }

line_element      = _{
    start ~ (
        page_break |
        note |
        boneyard |
        section_header |
        scene_heading |
        centered_action |
        scene_heading |
        transition |
        action
    ) ~ &NEWLINE
}
//multiline_element =  { dialogue }

// Title page
char           =  { (ASCII_ALPHANUMERIC | " " | "-" | "(" | ")" | "," | ".") }
key            = @{ char+ }
value          = @{ txt }
pair           =  { key ~ ":" ~ value }
multiline_pair =  { key ~ ":" ~ NEWLINE ~ block_value }
indentation    = _{ (" " | "\t")+ }
block_value    =  {
    PUSH(indentation) ~ value ~ NEWLINE ~ (PEEK ~ value ~ NEWLINE)* ~ DROP
}
// END title page

// Basics
ws         = _{ (" " | "\t") }
start      = _{ SOI | NEWLINE }
bn_open    =  { ("[[" | "/*") }
blank_line =  { NEWLINE }
txt        =  { (!(NEWLINE | bn_open) ~ ANY)+ }
text       =  _{ (boneyard | note | txt)+ }
emphasis   =  { "*"+ }

section_header = @{ "#"+ ~ ws* ~ text }

page_break = { "="{3, } }

// Scene headers
scene_heading = @{ NEWLINE ~ (force_scene | header_flag) ~ (!"#" ~ text) ~ scene_number? ~ &NEWLINE }
force_scene   = { "." ~ !"." }
scene_number  = { "#" ~ (ASCII_DIGIT | "." | "-")+ ~ "#" }
header_flag   = { ((^"INT" ~ "."? ~ ^"/EXT"?) | ^"EXT" | ^"EST" | ^"I" ~ "."? ~ ^"/E") ~ "."? ~ " " }

// Boneyards & Notes

boneyard     =  _{ "/*" ~ boneyard_txt ~ "*/" }
boneyard_txt =  _{ (boneyard | !"*/" ~ ANY)* }
note_txt     =  { (note | !"]]" ~ ANY)* }
note         =  { "[[" ~ note_txt ~ "]]" }

// Actions

action_text     = @{ text }
centered_action = @{ ">" ~ (!("<" | NEWLINE) ~ ANY)* ~ "<" }
forced_action   =  { "!" ~ action_text }
generic_action  =  { text }
action          =  { (forced_action | generic_action) }

// Characters and dialogue
dialogue          =  { start ~ character ~ (continuation_line | speech)+ }
lyrics            = @{ "~" ~ text ~ &NEWLINE }
speech            =  { (NEWLINE ~ ws* ~ (parenthetical | lyrics | text)) ~ &NEWLINE }
character_name    =  { (("@" ~ text) | ASCII_ALPHA_UPPER ~ (ASCII_ALPHA_UPPER | ASCII_DIGIT)*) }
dual_marker       =  { "^" }
continuation_line =  { NEWLINE ~ " "{2, } ~ &NEWLINE }
parenthetical     =  { "(" ~ (!")" ~ ANY)+ ~ ")" }
character         =  { ws* ~ character_name ~ ws* ~ (parenthetical | dual_marker)* ~ &NEWLINE }

// Transitions
transition           =  { emphasis? ~ (forced_transition | magic_transition | to_transition) ~ emphasis? ~ ws* }
forced_transition    = @{ ">" ~ transition_txt+ }
to_transition        = @{ leading* ~ "TO:" }
terminal_punctuation =  { ("." | ":") }
magic_phrases        =  { "CUT TO BLACK" | "FADE TO BLACK" | "FADE OUT" | "FADE IN" }
magic_transition     = @{ leading* ~ magic_phrases ~ terminal_punctuation? }
leading              =  { !("TO:" | magic_phrases) ~ (ws | transition_txt) }
transition_txt       =  { !(NEWLINE) ~ ANY }
