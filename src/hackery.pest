file = {
    SOI ~ ((page_break | scene_heading | lyrics | transition | dialogue | action | blank_line))+ ~ EOI
}

lyrics     = { NEWLINE ~ "~" ~ text ~ &NEWLINE }
page_break = { NEWLINE ~ "="{3, } ~ &NEWLINE }

// Basics
whitespace = _{ (" " | "\t") }
indent     = _{ whitespace }
blank_line =  { NEWLINE }
head       =  { !(NEWLINE | whitespace) ~ ANY }
tail       =  { !(NEWLINE) ~ ANY }
text       = @{ (emphasis | head ~ tail*) }

// emphasis
ul             =  { "_" }
underline      = @{ ul ~ (!ul ~ tail)+ ~ ul }
bi             =  { "***" }
bold_italics   = @{ bi ~ (!"*" ~ tail)+ ~ bi }
b              =  { "**" }
bold           = @{ b ~ (!"*" ~ tail)+ ~ b }
i              =  { "*" }
italics        = @{ i ~ (!"*" ~ tail)+ ~ i }
emphasis_chars =  { ul | bi | b | i }
emphasis       =  { underline | bold_italics | bold | italics }

// Scene headers
scene_heading = { NEWLINE ~ (force_scene | header_flag) ~ (!"#" ~ text) ~ scene_number? ~ &NEWLINE }
force_scene   = { "." }
scene_number  = { "#" ~ (ASCII_DIGIT | "." | "-")+ ~ "#" }
header_flag   = { (^"INT" | ^"EXT" | ^"EST" | ^"INT./EXT" | ^"INT/EXT" | ^"I/E") ~ (" " | ".")+ }

// Actions
dual_marker     =  { "^" }
action_text     = @{ (emphasis | tail+) }
centered_action =  { ">" ~ text ~ "<" }
forced_action   =  { "!" ~ action_text }
fallback_action =  { text }
action          =  { NEWLINE ~ (forced_action | centered_action | fallback_action) ~ &NEWLINE }

// Characters and dialogue
dialogue       = { NEWLINE ~ character ~ speech ~ &NEWLINE }
speech         = { (indent* ~ (parenthetical | emphasis | text) ~ NEWLINE?)+ }
character_name = { (("@" ~ text) | ASCII_ALPHA_UPPER+) }
parenthetical  = { "(" ~ (!")" ~ ANY)+ ~ ")" }
character      = { indent* ~ character_name ~ whitespace* ~ (parenthetical | dual_marker)* ~ NEWLINE }

// Transitions
transition        =  { NEWLINE ~ emphasis_chars? ~ (forced_transition | magic_transition | to_transition) ~ emphasis_chars? ~ &NEWLINE }
forced_transition = @{ ">" ~ transition_txt+ ~ ":"? }
to_transition     = @{ leading* ~ "TO:" }
magic_phrases     =  { "CUT TO BLACK" | "FADE TO BLACK" | "FADE OUT" | "FADE IN" }
magic_transition  = @{ leading* ~ magic_phrases ~ ":"? }
leading           =  { !("TO:" | magic_phrases) ~ (indent | transition_txt) }
transition_txt    =  { !(NEWLINE) ~ (ASCII_ALPHA_UPPER | ".") }
