file = {
    SOI ~ ((transition | page_break | lyrics | dialogue | action))+ ~ NEWLINE+ ~ EOI
}

lyrics = { "~" ~ text }

page_break = { NEWLINE ~ "="{3, } ~ &NEWLINE }

// Basics
whitespace  =  { (" " | "\t") }
indent      =  { whitespace }
blank_line  =  { NEWLINE }
head        =  { !(NEWLINE | whitespace) ~ ANY }
tail        =  { !(NEWLINE) ~ ANY }
text        = @{ head ~ tail* }
action_text = @{ (emphasis | tail+) }
dual_marker =  { "^" }

// emphasis
underline    = { "_" ~ (!"_" ~ text) ~ "_" }
bold_italics = { "***" ~ (!"*" ~ text) ~ "***" }
bold         = { "**" ~ (!"*" ~ text) ~ "**" }
italics      = { "*" ~ (!"*" ~ text) ~ "*" }
emphasis     = { underline | bold_italics | bold | italics }

// Scene headers
scene_heading = { NEWLINE ~ header_flag ~ whitespace+ ~ (!"#" ~ text) ~ scene_number ~ &NEWLINE }
scene_number  = { "#" ~ (ASCII_DIGIT | "." | "-")+ ~ "#" }
header_flag   = { ("." | ^"INT" | ^"EXT" | ^"EST" | ^"INT./EXT" | ^"INT/EXT" | ^"I/E") }

// Actions
centered_action = { ">" ~ text ~ "<" }
forced_action   = { "!" ~ action_text }
fallback_action = { text }
action          = { NEWLINE ~ (forced_action | centered_action | fallback_action) ~ &NEWLINE }

// Characters and dialogue
dialogue       = { NEWLINE ~ character ~ NEWLINE ~ text ~ &NEWLINE }
character_name = { (("@" ~ text) | ASCII_ALPHA_UPPER+) }
parenthetical  = { "(" ~ (!")" ~ ANY)+ ~ ")" }

character = { indent* ~ character_name ~ " "* ~ (parenthetical | dual_marker)* }

// Transitions
transition        =  { NEWLINE ~ (forced_transition | magic_transition | to_transition) ~ &NEWLINE }
forced_transition = @{ ">" ~ transition_txt+ }
to_transition     = @{ leading+ ~ "TO:" }
magic_phrases     =  { "CUT TO BLACK" | "FADE TO BLACK" | "FADE OUT" | "FADE IN" }
magic_transition  = @{ leading* ~ magic_phrases ~ transition_txt* }
leading           =  { !("TO:" | magic_phrases) ~ transition_txt }
transition_txt    =  { !(NEWLINE) ~ (ASCII_ALPHA_UPPER | "*" | "_" | "." | ":") }
